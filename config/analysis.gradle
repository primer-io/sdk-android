apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'io.gitlab.arturbosch.detekt'

// detekt setup - https://github.com/detekt/detekt
detekt {
    failFast = false
    buildUponDefaultConfig = true

    config = files("$rootDir/config/detekt.yml")

    debug = true
    parallel = true
    disableDefaultRuleSets = false

    ignoredBuildTypes = ["debug"]

//    config = files("$projectDir/config/detekt.yml")
//    baseline = file("$projectDir/config/baseline.xml")

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
        sarif.enabled = true // https://sarifweb.azurewebsites.net - for integrations with Github
    }
}

tasks.detekt.jvmTarget = projectJvmTarget

task detektProjectBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(rootDir))
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    baseline.set(file("$rootDir/config/detekt/baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

// ktlint setup - https://github.com/JLLeitschuh/ktlint-gradle
ktlint {
    debug = false
    verbose = false
    android = true
    outputToConsole = true
    ignoreFailures = false

    disabledRules = ["import-ordering"] // issue with false positives

    reporters {
        reporter "plain"
        reporter "json"
    }
}
