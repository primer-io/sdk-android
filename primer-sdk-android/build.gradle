plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.junit5
    alias libs.plugins.dexcount
}

apply from: "$rootDir/config/android-common.gradle"

android {
    namespace 'io.primer.android'

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled true

            consumerProguardFiles 'consumer-rules.pro'
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SDK_VERSION_STRING', "\"$VERSION_NAME\""
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        freeCompilerArgs += ['-Xjvm-default=all', '-Xcontext-receivers']
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation libs.networking.okhttp

    implementation libs.kotlin.stdlib
    implementation libs.kotlin.coroutines
    implementation libs.android.ktx
    implementation libs.android.appcompat

    implementation libs.android.material
    implementation libs.android.constraintlayout

    implementation libs.android.fragment

    implementation libs.android.startup

    implementation libs.android.lifecycle.livedata.ktx
    implementation libs.android.lifecycle.runtime.ktx
    implementation libs.android.lifecycle.viewmodel.state
    implementation libs.android.lifecycle.viewmodel.ktx

    implementation libs.play.services.wallet

    compileOnly libs.primer.threeds
    testImplementation libs.primer.threeds

    compileOnly libs.primer.klarna
    testImplementation libs.primer.klarna

    compileOnly libs.primer.nol.pay
    testImplementation libs.primer.nol.pay

    compileOnly libs.primer.ipay88
    testImplementation libs.primer.ipay88

    compileOnly libs.primer.stripe
    testImplementation libs.primer.stripe
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://x.klarnacdn.net/mobile-sdk/'
    }
    maven {
        url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
    }
    maven { // TODO TWS: remove before release
        url "https://s01.oss.sonatype.org/content/repositories/staging/"
    }
}

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

dexcount {
    format = com.getkeepsafe.dexcount.OutputFormat.JSON
    includeClasses = false
    orderByMethodCount = true
    maxTreeDepth = 5
}


apply from: "$rootDir/config/coverage/kover.gradle"
apply from: "$rootDir/config/code-analysis/static-analysis.gradle"
apply from: "$rootDir/config/code-analysis/lint.gradle"
apply from: "$rootDir/config/publish.gradle"
apply from: "$rootDir/config/code-analysis/sonar.gradle"
