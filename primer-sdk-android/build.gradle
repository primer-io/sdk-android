import com.getkeepsafe.dexcount.OutputFormat

buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.5'
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.getkeepsafe.dexcount'
}

apply plugin: 'com.dicedmelon.gradle.jacoco-android'

apply from: "$rootDir/config/android-common.gradle"
apply from: "$rootDir/config/test-dependencies.gradle"

android {
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled true

            consumerProguardFiles 'consumer-rules.pro'
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SDK_VERSION_STRING', "\"$VERSION_NAME\""
    }
    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        freeCompilerArgs += '-Xjvm-default=all'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // mut be added for Java interop, as it is required on compile classpath
    api "io.insert-koin:koin-android:$koinVersion"

    implementation Dependencies.Networking.okhttp
    implementation Dependencies.Networking.okhttpLoggingInterceptor

    implementation Dependencies.Kotlin.stdlib
    implementation Dependencies.Kotlin.coroutines
    implementation AndroidDependencies.coreKtx
    implementation AndroidDependencies.appCompat
    implementation AndroidDependencies.material
    implementation AndroidDependencies.constraintLayout

    implementation AndroidDependencies.fragment
    implementation AndroidDependencies.startup

    implementation AndroidDependencies.Lifecycle.liveData
    implementation AndroidDependencies.Lifecycle.lifecycle
    implementation AndroidDependencies.Lifecycle.viewModel
    implementation AndroidDependencies.Lifecycle.viewModelSavedState

    implementation 'com.google.android.gms:play-services-wallet:19.1.0'

    compileOnly Dependencies.ThreeDs.primer3ds
    testImplementation Dependencies.ThreeDs.primer3ds

    compileOnly Dependencies.Klarna.primerKlarna
    testImplementation Dependencies.Klarna.primerKlarna

    compileOnly Dependencies.iPay88.primeriPay88
    testImplementation Dependencies.iPay88.primeriPay88

    testImplementation TestDependencies.Kotlin.coroutines
    testImplementation TestDependencies.Android.archCore
    testImplementation TestDependencies.Android.liveData
    testImplementation TestDependencies.Koin.test
    testImplementation TestDependencies.Serialization.json

    androidTestImplementation TestDependencies.Android.Espresso.core
}

repositories {
    google()
    mavenCentral()
    maven {
        url "${ARTIFACTORY_3DS_URL}"
    }
    maven {
        url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
    }
    mavenLocal()
    maven {
        url 'https://x.klarnacdn.net/mobile-sdk/'
    }
}

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
    tasks["sonarqube"].dependsOn "jacocoTestReleaseUnitTestReport"
    tasks["jacocoTestReleaseUnitTestReport"].dependsOn "testReleaseUnitTest"
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoAndroidUnitTestReport {
    excludes += [
            '**/example/**',
            '**/di/**',
            '**/ui/**',
            '**/analytics/**',
    ]
}

dexcount {
    format = OutputFormat.JSON
    includeClasses = false
    orderByMethodCount = true
    maxTreeDepth = 5
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}


apply from: "$rootDir/config/analysis.gradle"
apply from: "$rootDir/config/publish.gradle"
